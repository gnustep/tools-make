AC_INIT(application.make)

#
#   configure.in
#
#   Copyright (C) 1997 Free Software Foundation, Inc.
#
#   Author:  Scott Christley <scottc@net-community.com>
#	     Ovidiu Predescu <ovidiu@net-community.com>
#
#   This file is part of the GNUstep Makefile Package.
#
#   This library is free software; you can redistribute it and/or
#   modify it under the terms of the GNU General Public License
#   as published by the Free Software Foundation; either version 2
#   of the License, or (at your option) any later version.
#   
#   You should have received a copy of the GNU General Public
#   License along with this library; see the file COPYING.LIB.
#   If not, write to the Free Software Foundation,
#   59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

AC_CONFIG_HEADER(config.h)

#--------------------------------------------------------------------
# The GNUstep root directory.
# The user should have the GNUSTEP_SYSTEM_ROOT environment variable
# defined, but if not then we have a global default.
#--------------------------------------------------------------------
AC_PREFIX_DEFAULT(`if test "x$GNUSTEP_SYSTEM_ROOT" = "x"; then 
echo /usr/GNUstep ;
else
echo $GNUSTEP_SYSTEM_ROOT ;
fi`)

#--------------------------------------------------------------------
# Determine the host, build, and target systems
#--------------------------------------------------------------------
AC_CANONICAL_SYSTEM

AC_SUBST(program_suffix)

#--------------------------------------------------------------------
# Find the binary and compile tools
#--------------------------------------------------------------------
AC_PROG_CC
AC_PROG_CPP
AC_PROG_RANLIB

AC_PROG_INSTALL
# HOST_INSTALL is the name of the install program in config.make so set it up
# to point to the install-sh script in the GNUstep tree if no system install is
# found.
AC_SUBST(HOST_INSTALL)
if test "$INSTALL" = "$ac_install_sh"; then
  HOST_INSTALL=$prefix/Makefiles/$INSTALL
else
  HOST_INSTALL=$INSTALL
fi

AC_LN_S

#--------------------------------------------------------------------
# Find for X windows
#--------------------------------------------------------------------
AC_PATH_X
X_INCLUDE=""
if test -n "$x_includes"; then
    X_INCLUDE="-I$x_includes"
fi
X_LIBS=""
if test -n "$x_libraries"; then
    X_LIBS="-L$x_libraries"
fi
if test -r $x_includes/X11/DPS; then
  X_INCLUDE="$X_INCLUDE -I$x_includes/X11"
fi

AC_SUBST(X_INCLUDE)
AC_SUBST(X_LIBS)

AC_HEADER_DIRENT
AC_HAVE_HEADERS(sys/param.h sys/file.h dir.h string.h stdlib.h sys/types.h dnl
		sys/stat.h sys/errno.h)


#--------------------------------------------------------------------
# Find for DPS
#--------------------------------------------------------------------
if test -f clean_cpu.sh; then
  transformed_host_cpu=`./clean_cpu.sh $host_cpu`
  transformed_target_cpu=`./clean_cpu.sh $target_cpu`
else if test -f ${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_cpu.sh; then
  transformed_host_cpu=`${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_cpu.sh $host_cpu`
  transformed_target_cpu=`${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_cpu.sh $target_cpu`
fi
fi

if test -f clean_os.sh; then
  transformed_host_os=`./clean_os.sh $host_os`
  transformed_target_os=`./clean_os.sh $target_os`
else if test -f ${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_os.sh; then
  transformed_host_os=`${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_os.sh $host_os`
  transformed_target_os=`${GNUSTEP_SYSTEM_ROOT}/Makefiles/clean_os.sh $target_os`
fi
fi

ac_cv_file_output_directory=Source/$transformed_host_cpu/$transformed_host_os

# Add the target header file directory as an include path so that the
# check for dpsNXargs.h below looks at the appropriate target includes
ORIG_CPPFLAGS=${CPPFLAGS}
CPPFLAGS="-I${GNUSTEP_SYSTEM_ROOT}/Headers/$transformed_target_cpu/$transformed_target_os ${CPPFLAGS}"
CPPFLAGS="-I${GNUSTEP_LOCAL_ROOT}/Headers/$transformed_target_cpu/$transformed_target_os ${CPPFLAGS}"
CPPFLAGS="-I${GNUSTEP_USER_ROOT}/Headers/$transformed_target_cpu/$transformed_target_os ${CPPFLAGS}"
CPPFLAGS="${CPPFLAGS} ${X_INCLUDE}"

# We care about this header file on the target not host system
AC_HAVE_HEADERS(DPS/dpsclient.h, DPS_DEFINE=' -DHAVE_DPS_CLIENT_H=1',
	DPS_DEFINE=' -DHAVE_DPS_CLIENT_H=0')

# Restore back to the original
CPPFLAGS=${ORIG_CPPFLAGS}

AC_SUBST(DPS_DEFINE)

#--------------------------------------------------------------------
# Setup the library combination
#--------------------------------------------------------------------
AC_ARG_WITH(library-combo,[
--with-library-combo
    Define the default library combinations that will be used when you
type 'make'. You can still change the library combination at runtime
by specifying the 'library_combo' make variable. For example:
    
      $ make library_combo=nx-pdo-nx-nil
],
ac_cv_library_combo=$withval,
ac_cv_library_combo=$ac_cv_library_combo
)

if test "$ac_cv_library_combo" = ""; then
  if test $host_os = nextstep4; then
    ac_cv_library_combo=nx-nx-nx-nil
  else
    ac_cv_library_combo=gnu-gnu-gnu-xdps
  fi
fi
AC_SUBST(ac_cv_library_combo)

#--------------------------------------------------------------------
# Check if libobjc was compiled with thread support.
#--------------------------------------------------------------------
AC_MSG_CHECKING(whether objc has thread support)
saved_LIBS="$LIBS"
objc_thread_lib=""
if test $host_os = linux-gnu; then
  LIBS="-lobjc -lpthread"
  AC_TRY_RUN([#include "config_thread.m"], objc_threaded="-lpthread",
	objc_threaded="", objc_threaded="-lpthread")
  if test x"$objc_threaded" = x""; then
    LIBS="-lobjc -lpcthread"
    AC_TRY_RUN([#include "config_thread.m"], objc_threaded="-lpcthread",
	objc_threaded="", objc_threaded="-lpcthread")
  fi
else
  LIBS="-lobjc $LIBS"
  AC_TRY_RUN([#include "config_thread.m"], objc_threaded="-lthread",
	objc_threaded="", objc_threaded="-lthread")
fi
LIBS="$saved_LIBS"
if test x"$objc_threaded" = x""; then
  AC_MSG_RESULT(no)
else
  AC_MSG_RESULT(yes)
fi
AC_SUBST(objc_threaded)

#--------------------------------------------------------------------
# Produce the output files
#--------------------------------------------------------------------
AC_OUTPUT(config.h config.make openapp debugapp GNUmakefile
GNUstep.sh GNUstep.csh,
chmod a+x openapp debugapp)
